
Test "Simple aToken Transfer"
    NewComptroller
    ListedAToken ZRX aZRX initialExchangeRate:1e9
    Prep Geoff Some ZRX aZRX
    Mint Geoff 50e18 aZRX
    -- Just to be sure, check initial balances
    Assert Equal (Bep20 aZRX TokenBalance Geoff) (Exactly 50e9)
    Assert Equal (Bep20 aZRX TokenBalance Torrey) Zero
    -- Just transfer
    Transfer Geoff Torrey 10e9 aZRX
    Assert Equal (Bep20 aZRX TokenBalance Geoff) (Exactly 40e9)
    Assert Equal (Bep20 aZRX TokenBalance Torrey) (Exactly 10e9)

Test "Simple aToken Transfer When Underlying Paused"
    NewComptroller
    ListedAToken WBTC aWBTC initialExchangeRate:0.1 tokenType:WBTC
    Prep Geoff Some WBTC aWBTC
    Mint Geoff 50e8 aWBTC
    -- Just to be sure, check initial balances
    Assert Equal (Bep20 aWBTC TokenBalance Geoff) (Exactly 50e9)
    Assert Equal (Bep20 aWBTC TokenBalance Torrey) Zero
    -- Just transfer
    Bep20 WBTC Pause
    Transfer Geoff Torrey 10e9 aWBTC
    Assert Equal (Bep20 aWBTC TokenBalance Geoff) (Exactly 40e9)
    Assert Equal (Bep20 aWBTC TokenBalance Torrey) (Exactly 10e9)

Test "Simple aToken Transfer 1:1 Rate"
    NewComptroller
    ListedAToken ZRX aZRX initialExchangeRate:1e0
    Prep Geoff Some ZRX aZRX
    Mint Geoff 50e18 aZRX
    -- Just to be sure, check initial balances
    Assert Equal (Bep20 aZRX TokenBalance Geoff) (Exactly 50e18)
    Assert Equal (Bep20 aZRX TokenBalance Torrey) Zero
    -- Just transfer
    Transfer Geoff Torrey 10e18 aZRX
    Assert Equal (Bep20 aZRX TokenBalance Geoff) (Exactly 40e18)
    Assert Equal (Bep20 aZRX TokenBalance Torrey) (Exactly 10e18)

Test "Simple aToken Transfer Not Allowed by Comptroller"
    NewComptroller
    ListedAToken ZRX aZRX initialExchangeRate:1e0
    Comptroller SetCollateralFactor aZRX 0.1
    EnterMarkets Geoff aZRX
    Prep Geoff Some ZRX aZRX
    Mint Geoff 50e18 aZRX
    -- Just to be sure, check initial balances
    Invariant Remains (Bep20 aZRX TokenBalance Geoff) (Exactly 50e18)
    Invariant Remains (Bep20 aZRX TokenBalance Torrey) Zero
    -- Just transfer
    AllowFailures
    Transfer Geoff Torrey 60e18 aZRX
    Assert Failure COMPTROLLER_REJECTION TRANSFER_COMPTROLLER_REJECTION INSUFFICIENT_LIQUIDITY

Test "Simple aToken Transfer From"
    NewComptroller
    ListedAToken ZRX aZRX initialExchangeRate:1e9
    Prep Geoff Some ZRX aZRX
    Mint Geoff 50e18 aZRX
    -- Just to be sure, check initial balances
    Invariant Remains (Bep20 aZRX TokenBalance Torrey) Zero
    Assert Equal (Bep20 aZRX TokenBalance Geoff) (Exactly 50e9)
    Assert Equal (Bep20 aZRX TokenBalance Coburn) Zero
    -- Add approval
    From Geoff (Bep20 aZRX Approve Torrey 25e9)
    Assert Equal (Bep20 aZRX Allowance Geoff Torrey) (Exactly 25e9)
    -- Perform transfer from
    From Torrey (Bep20 aZRX TransferFrom Geoff Coburn 10e9)
    Assert Equal (Bep20 aZRX TokenBalance Geoff) (Exactly 40e9)
    Assert Equal (Bep20 aZRX TokenBalance Coburn) (Exactly 10e9)
    Assert Equal (Bep20 aZRX Allowance Geoff Torrey) (Exactly 15e9)

Test "aToken Transfer From Not Allowed"
    NewComptroller
    ListedAToken ZRX aZRX initialExchangeRate:1e9
    Prep Geoff Some ZRX aZRX
    Mint Geoff 50e18 aZRX
    --
    Invariant Remains (Bep20 aZRX TokenBalance Geoff) (Exactly 50e9)
    Invariant Remains (Bep20 aZRX TokenBalance Torrey) (Exactly 0e9)
    AllowFailures
    Bep20 aZRX TransferFrom Geoff Torrey 10e9
    Assert Failure MATH_ERROR TRANSFER_NOT_ALLOWED

Test "aToken Transfer paused"
    NewComptroller
    ListedAToken ZRX aZRX initialExchangeRate:1e9
    Prep Geoff Some ZRX aZRX
    Mint Geoff 50e18 aZRX
    -- Just to be sure, check initial balances
    Assert Equal (Bep20 aZRX TokenBalance Geoff) (Exactly 50e9)
    Assert Equal (Bep20 aZRX TokenBalance Coburn) Zero
    -- Pause and attempt transfer
    Comptroller SetPauseGuardian Coburn
    From Coburn (Comptroller SetGuardianPaused "Transfer" True)
    AllowFailures
    Transfer Geoff Torrey 10e9 aZRX
    Assert Revert "revert transfer is paused"
    Assert Equal (Bep20 aZRX TokenBalance Geoff) (Exactly 50e9)
    Assert Equal (Bep20 aZRX TokenBalance Coburn) Zero
    -- unPause and complete transfer
    Invariant Success
    Comptroller SetGuardianPaused "Transfer" False
    Transfer Geoff Coburn 10e9 aZRX
    Assert Equal (Bep20 aZRX TokenBalance Geoff) (Exactly 40e9)
    Assert Equal (Bep20 aZRX TokenBalance Coburn) (Exactly 10e9)
