Macro DeployComptroller price=1.0
    Unitroller Deploy
    PriceOracle Deploy Fixed price
    PriceOracleProxy Deploy Admin (PriceOracle Address) (Address Zero) (Address Zero) (Address Zero) (Address Zero) (Address Zero)
    ComptrollerImpl Deploy Scenario ComptrollerScen
    Unitroller SetPendingImpl ComptrollerScen
    ComptrollerImpl ComptrollerScen Become
    Comptroller SetPriceOracle (PriceOracleProxy Address)
    Comptroller SetCloseFactor 0.5
    Comptroller LiquidationIncentive 1.1

Macro SetupMarkets
    NewAToken ZRX aZRX
    NewAToken BAT aBAT
    Support aZRX collateralFactor:0.5
    Support aBAT collateralFactor:0.5

Macro SetupCompToken atlantisInitAmount=5000000e18
    Bep20 Deploy Standard Atlantis "Atlantis Token" 18
    Give (Address Comptroller) atlantisInitAmount Atlantis
    Comptroller Send "setAtlantisAddress(address)" (Address Atlantis)

Macro FlywheelComptroller price=1.0 borrowRate=0.000005 atlantisInitAmount=5000000e18
    DeployComptroller price
    SetupMarkets
    SetupCompToken atlantisInitAmount

Macro InitUsage
    Prep Geoff 100e18 ZRX aZRX
    Mint Geoff 50e18 aZRX--tokenbalance = 50e18 / 2e9 = 2.5e10
    Prep Coburn Some BAT aBAT
    Mint Coburn 6e18 aBAT--tokenbalance = 6e18 / 2e9 = 3e9
    EnterMarkets Coburn aBAT
    Borrow Coburn 1e18 aZRX

Macro InitUsageAndSpeeds
    InitUsage
    Comptroller SetAtlantisSpeeds (aZRX aBAT) (1 1) (1 1)

Test "Atlantis can be granted in combination with liquidity rewards"
    FlywheelComptroller
    InitUsageAndSpeeds
    Assert Equal (Comptroller AtlantisAccrued Geoff) 0
    Assert Equal (Bep20 Atlantis TokenBalance Geoff) 0
    FastForward 1000 Blocks
    Comptroller ClaimAtlantis Geoff
    Comptroller Send "_grantAtlantis(address,uint256)" (Address Geoff) 1000
    Assert Equal (Comptroller AtlantisAccrued Geoff) 0
    Assert Equal (Bep20 Atlantis TokenBalance Geoff) 2000 -- 1000 (grant) + 1000 (Atlantis supply rewards)

Test "Atlantis can be granted"
    -- Can be granted once
    FlywheelComptroller
    InitUsageAndSpeeds
    Assert Equal (Comptroller AtlantisAccrued Geoff) 0
    Assert Equal (Bep20 Atlantis TokenBalance Geoff) 0
    Comptroller Send "_grantAtlantis(address,uint256)" (Address Geoff) 1000
    Assert Equal (Comptroller AtlantisAccrued Geoff) 0
    Assert Equal (Bep20 Atlantis TokenBalance Geoff) 1000
    -- Assert Log CompGranted (recipient (Address Geoff)) (amount "1000")
    -- Can be granted multiple times
    Comptroller Send "_grantAtlantis(address,uint256)" (Address Geoff) 2000
    Assert Equal (Comptroller AtlantisAccrued Geoff) 0
    Assert Equal (Bep20 Atlantis TokenBalance Geoff) 3000

Test "Atlantis can be streamed to contributors"
    FlywheelComptroller
    InitUsageAndSpeeds
    Assert Equal (Comptroller AtlantisAccrued Torrey) 0
    Assert Equal (Bep20 Atlantis TokenBalance Torrey) 0
    Comptroller Send "_setContributorAtlantisSpeed(address,uint256)" (Address Torrey) 300
    -- Assert Log ContributorCompSpeedUpdated (recipient (Address Torrey)) (amount "300")
    FastForward 1000 Blocks
    -- Just claimComp does not receive Atlantis
    Comptroller ClaimAtlantis Torrey
    Assert Equal (Comptroller AtlantisAccrued Torrey) 0
    Assert Equal (Bep20 Atlantis TokenBalance Torrey) 0
    -- Calling updateContributorRewards and then claimComp receives Atlantis
    Comptroller UpdateContributorRewards Torrey
    Assert Equal (Comptroller AtlantisAccrued Torrey) 300000
    Comptroller ClaimAtlantis Torrey
    Assert Equal (Comptroller AtlantisAccrued Torrey) 0
    Assert Equal (Bep20 Atlantis TokenBalance Torrey) 300000

Test "Atlantis can be streamed in combination with liquidity rewards"
    FlywheelComptroller
    InitUsageAndSpeeds
    Comptroller Send "_setContributorAtlantisSpeed(address,uint256)" (Address Geoff) 300
    FastForward 1000 Blocks
    --Just claimComp does not receive Atlantis
    Comptroller UpdateContributorRewards Geoff
    Assert Equal (Comptroller AtlantisAccrued Geoff) 300000
    Comptroller ClaimAtlantis Geoff
    Assert Equal (Comptroller AtlantisAccrued Geoff) 0
    Assert Equal (Bep20 Atlantis TokenBalance Geoff) 301000 -- 300000 (contributer grant) + 1000 (Atlantis supply rewards)

Test "Atlantis streaming can be changed for contributors"
    FlywheelComptroller
    InitUsageAndSpeeds
    Comptroller Send "_setContributorAtlantisSpeed(address,uint256)" (Address Torrey) 300
    FastForward 1000 Blocks
    Comptroller Send "_setContributorAtlantisSpeed(address,uint256)" (Address Torrey) 600
    FastForward 1000 Blocks
    Comptroller UpdateContributorRewards Torrey
    Comptroller ClaimAtlantis Torrey
    Assert Equal (Comptroller AtlantisAccrued Torrey) 0
    Assert Equal (Bep20 Atlantis TokenBalance Torrey) 900000
    Comptroller Send "_setContributorAtlantisSpeed(address,uint256)" (Address Torrey) 0
    FastForward 2000 Blocks
    Assert Equal (Comptroller AtlantisAccrued Torrey) 0
    Assert Equal (Bep20 Atlantis TokenBalance Torrey) 900000
